plugins {
    id 'java'
    id 'org.gradle.test-retry' version '1.6.4'
}

apply from: 'gradle/send-metrics-to-elastic.gradle'

group = 'io.github.alexshamrai'
version = '1.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.11.4')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.junit.platform:junit-platform-launcher'
    testImplementation 'io.github.alexshamrai:junit-ctrf-reporter:0.4.5'
}

test {
    systemProperties += System.properties.findAll { k, v -> k.toString().startsWith("ctrf") }

    useJUnitPlatform {
        if (project.hasProperty("includedTags")) {
            def includedTags = project.properties.get("includedTags").toString().split(',')
            includedTags.each { tag ->
                println "Including tag: $tag"
                includeTags(tag.trim())
            }
        }

        if (project.hasProperty("excludedTags")) {
            def excludedTags = project.properties.get("excludedTags").toString().split(',')
            excludedTags.each { tag ->
                println "Excluding tag: $tag"
                excludeTags(tag.trim())
            }
        }
    }

    retry {
        maxRetries = 1
        maxFailures = 1000
        failOnPassedAfterRetry = false
    }

    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }
}